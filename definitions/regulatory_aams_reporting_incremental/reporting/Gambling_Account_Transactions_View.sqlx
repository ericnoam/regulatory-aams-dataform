config {
    type: 'view',
    description: "Incremental aggregate view for the Gambling Account Transactions Report",
    columns: {
        ID_FSC: "Unique identifier of the FSC that is handling the transaction",
        NETWORK_ID: "Unique identifier of the CN’s Sales Network that is handling the transaction (see network code table)",
        CN_ID: "Uniqueidentifier of the CN that is handling the transaction",
        Transaction_ID: "Unique identifier of the transaction (at CN level)",
        Account_NETWORK_ID: "Unique identifier of the CN’s Sales Network with which the client has set up the Gambling Account (see network code table)",
        Account_Code: "Unique Gambling Account Code for the CN",
        Payment_Method: "Code of the payment methods used (see payment methods code table)",
        Transaction_Reason: "Bonus transaction description of the Gambling Account (see transaction description code table)",
        Transaction_Amount: "Amount of the transaction on the Gambling Account (in euro cents)",
        Balance_Amount: "Gambling Account Balance including the transaction made and the bonuses found on the Gambling Account (in euro cents)",
        Data_Ora_Saldo: "Data and Time of the Balance",
        _ts_ms_as_timestamp: "Timestamp of balance"
    }
}

SELECT
  "${dataform.projectConfig.vars.fsc_id}" as ID_FSC
  , ${dataform.projectConfig.vars.network_id} as NETWORK_ID
  , "${dataform.projectConfig.vars.cn_id}" as CN_ID
  , pay.Regulatory_ID as Transaction_ID
  , ${dataform.projectConfig.vars.account_network_id} as Account_NETWORK_ID
  , ams.AamsPlayerId as Account_Code
  , pay.Method_ID as Payment_Method
  , pay.Type as Transaction_Reason
  , pay.Amount as Transaction_Amount
  , act.BalanceAfter as Balance_Amount
  , act._ts_ms_as_timestamp as Data_Ora_Saldo
  , act._ts_ms_as_timestamp
  FROM ${ref('Gambling_Account_Transactions_Model_Inc_Account')} ams
JOIN ${ref('Gambling_Account_Transactions_Model_Inc_Payment')} pay
    ON ams.Player_UID = pay.Player_UID
JOIN ${ref('Gambling_Account_Transactions_Model_Inc_Account_Transaction')} act
    ON ams.Id = act.Account_Id
